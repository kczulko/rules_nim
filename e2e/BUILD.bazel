load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "default_test_runner",
    "integration_test_utils",
)
load(":defs.bzl", "bazel_integration_spec")

default_test_runner(
    name = "execute_tests",
)

default_test_runner(
    name = "execute_binary",
    bazel_cmds = [
        "build //...",
        "run :bin",
    ],
)

TESTS = {
    "nim_cc_binary": ":execute_binary",
    "nim_cc_test": ":execute_tests",
    "nim_cc_library": ":execute_tests",
    "numericalnim": ":execute_tests",
    "c_invocation": ":execute_tests",
    "nim_invocation_from_c": ":execute_tests",
}

[
    bazel_integration_spec(
        test_runner = test_runner,
        workspace_path = workspace_path,
    )
    for workspace_path, test_runner in TESTS.items()
]

test_suite(
    name = "all_integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = [
        "{}_spec".format(test_name)
        for test_name in TESTS.keys()
    ],
    visibility = ["//:__subpackages__"],
)

bzl_library(
    name = "defs",
    srcs = ["defs.bzl"],
    visibility = ["//visibility:public"],
    deps = [
        "@bazel_binaries//:defs",
        "@rules_bazel_integration_test//bazel_integration_test:defs",
    ],
)
